// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
	alias libs.plugins.android.application apply false
	alias libs.plugins.android.library apply false
	alias libs.plugins.kotlinAndroid apply false
	alias libs.plugins.mavenPublish apply false
	alias libs.plugins.ubique.alpaka apply false
	alias libs.plugins.ubique.preset apply false
}

task clean(type: Delete) {
	delete rootProject.buildDir
}

allprojects {
	group = property("GROUP").toString()
	version = getProjectVersion()

	plugins.withId("com.vanniktech.maven.publish") {
		mavenPublishing {
			pom {
				group = project.group
				version = project.version
			}
		}
	}


	// https://github.com/Kotlin/dokka/issues/2956
	tasks.matching { task ->
		task.name.contains("javaDocReleaseGeneration")
	}.configureEach {
		enabled = false
	}
}

private String getProjectVersion() {
	def versionFromGradleProperties = property("VERSION").toString()
	def versionFromWorkflow = null
	try {
		versionFromWorkflow = property("githubRefName").toString().replaceFirst("^v", "")
	} catch (MissingPropertyException ignored) {
		// Ignore exception and continue
	}
	return versionFromWorkflow ?: versionFromGradleProperties
}